apply plugin: 'com.android.application'

project.ext {
    GOLGI_DIR = "$System.env.HOME" + '/Golgi-Pkg/LATEST'
    GOLGI_THRIFT_FILE = "../../Doorman.thrift"
    GOLGI_DEV_KEY_FILE = "../../Golgi.DevKey"
    GOLGI_APP_KEY_FILE = "../../Golgi.AppKey"
    GOLGI_GEN_DIR = "src/main/java/io/golgi/example/doorman/gen"
    GOLGI_GEN_PKG = "io.golgi.example.doorman.gen"
}

apply from: project.GOLGI_DIR + '/Android/golgi.gradle'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "io.golgi.example.doorman"
        minSdkVersion 16
        targetSdkVersion 20
        versionCode 4
        versionName "1.3"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


task getVariables(){
    File file = project.file("../../Doorman.conf")
    int idx

    file.eachLine{
        if(it.startsWith('ADDR')){
            idx = it.indexOf('=')
            project.ext.set('DoormanAddress',it.substring(idx+1).trim())
        }
        else if(it.startsWith('LAT')){
            idx = it.indexOf('=')
            project.ext.set('DoormanLatitude',it.substring(idx+1).trim())
        }
        else if(it.startsWith('LON')){
            idx = it.indexOf('=')
            project.ext.set('DoormanLongitude',it.substring(idx+1).trim())
        }
    }
}

task completeDoorman(type: Copy, dependsOn: getVariables) {
    outputs.upToDateWhen {false}
    from 'src/main/java/io/golgi/example/doorman/Doorman.java.default'
    into 'src/main/java/io/golgi/example/doorman'

    // rename the file
    rename(/(.+)\.default/, '$1')

    // substitute the copyright token
    expand('DoormanAddress': project.DoormanAddress)
}

task completeSendKeyRequestDialogFragment(type: Copy, dependsOn: getVariables){
    outputs.upToDateWhen {false}
    from 'src/main/java/io/golgi/example/doorman/SendKeyRequestDialogFragment.java.default'
    into 'src/main/java/io/golgi/example/doorman/'

    String ac = project.DoormanAddress
    int idx = ac.indexOf('@')
    String av = ac.substring(idx);

    rename(/(.+)\.default/,'$1')
    expand('AutoComplete': ac, 'AutoValue': av)
}

task completeDoormanService(type: Copy, dependsOn: getVariables){
    outputs.upToDateWhen {false}
    from 'src/main/java/io/golgi/example/doorman/DoormanService.java.default'
    into 'src/main/java/io/golgi/example/doorman'

    rename(/(.+)\.default/,'$1')
    expand('DoormanLatitude': project.DoormanLatitude,'DoormanLongitude': project.DoormanLongitude)
}

preBuild.dependsOn completeDoorman, completeSendKeyRequestDialogFragment, completeDoormanService

task removeGeneratedSendKeyRequestDialogFragment(type: Delete){
    outputs.upToDateWhen {false}
    delete 'src/main/java/io/golgi/example/doorman/SendKeyRequestDialogFragment.java'
}

task removeGeneratedDoorman(type: Delete) {
    outputs.upToDateWhen {false}
    delete 'src/main/java/io/golgi/example/doorman/Doorman.java'
}

clean.dependsOn removeGeneratedDoorman, removeGeneratedSendKeyRequestDialogFragment

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    wearApp project(':wear')
    compile 'com.google.android.gms:play-services:5.+'
    compile 'com.android.support:appcompat-v7:20.+'
    compile project.GOLGI_AAR
}
